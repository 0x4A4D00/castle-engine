{
  Copyright 2003-2010 Michalis Kamburelis.

  This file is part of "Kambi VRML game engine".

  "Kambi VRML game engine" is free software; see the file COPYING.txt,
  included in this distribution, for details about the copyright.

  "Kambi VRML game engine" is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

  ----------------------------------------------------------------------------
}

{ Low-level demo how to access TVRMLGLScene without a SceneManager, part 1.
  This is highly discouraged now --- SceneManager does a lot of stuff
  for you, results in more functional and at the same time shorter code.
  This demo remains just to show that it's possible (even if
  uncomfortable) to work without the SceneManager.

  It loads a model from a filename given as command-line parameter.
  Supports all known model formats (VRML, X3D, Collada etc.,
  for a full list of supported formats see view3dscene documentation).

  It allows user to walk/examine the scene, see view3dscene documentation
  for keys/mouse controls available,
  [http://vrmlengine.sourceforge.net/view3dscene.php#section_keys].

  Thanks to using TGLUIWindow class, that descends
  from TGLWindowDemo, this program automatically handles keys Escape (Quit)
  and F11 (Swap fullscreen) and displays FPS on window's Caption.

  For the sake of simplicity, this example takes some shortcuts
  (that are fixed in the direct_vrmlglscene_test_2.pasprogram):
  - Collision detection is *not* done.
    We even turn off gravity (because it would make viewer fall down infinitely,
    since without collision detection you sink through the floor.)
  - Scene is rendered without frustum culling, since we do not pass
    current frustum to Scene.Render.
  - If you will load large VRML scene, then right after program start
    you may notice for a very short time that your camera moves too fast.
}

program direct_vrmlglscene_test_1;

uses VectorMath, VRMLNodes, VRMLOpenGLRenderer, GL, GLU, GLWindow,
  KambiClassUtils, KambiUtils, SysUtils, Classes,
  KambiGLUtils, VRMLGLScene, Cameras, KambiFilesUtils, VRMLErrors;

var
  Glw: TGLUIWindow;
  Scene: TVRMLGLScene;

procedure Draw(Glwin: TGLWindow);
begin
  glClear(GL_COLOR_BUFFER_BIT or GL_DEPTH_BUFFER_BIT);
  glLoadMatrix(glw.Camera.Matrix);
  Scene.Render(nil, tgAll);
end;

procedure Init(Glwin: TGLWindow);
begin
  glEnable(GL_LIGHT0); { headlight }
end;

procedure Close(Glwin: TGLWindow);
begin
  Scene.GLContextClose;
end;

procedure Resize(Glwin: TGLWindow);
begin
  Scene.GLProjection(Glw.Camera, Scene.BoundingBox,
    Glwin.Width, Glwin.Height);
end;

begin
  Glw := TGLUIWindow.Create(Application);

  Parameters.CheckHigh(1);

  VRMLWarning := @VRMLWarning_Write;
  Scene := TVRMLGLScene.Create(nil);
  try
    Scene.Load(Parameters[1]);

    Writeln(Scene.Info(true, true, false));

    { init camera }
    Glw.Camera := Scene.CreateCamera(Glw);
    if Glw.Camera is TWalkCamera then
      (Glw.Camera as TWalkCamera).Gravity := false;

    Glw.OnInit := @Init;
    Glw.OnClose := @Close;
    Glw.OnResize := @Resize;
    Glw.InitAndRun(ProgramName, @Draw);
  finally Scene.Free end;
end.
