{
  Copyright 2003-2008 Michalis Kamburelis.

  This file is part of "Kambi VRML game engine".

  "Kambi VRML game engine" is free software; see the file COPYING.txt,
  included in this distribution, for details about the copyright.

  "Kambi VRML game engine" is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
}

{ Demo of fog culling.

  1. It always loads models/fog_culling_final.wrl VRML file.
     Be sure to run it with proper current directory (examples/vrml/).

  2. It handles Background node (because our models/fog_culling_final.wrl
     model uses it).

  3. It handles some keys:
     'f' key turns fog on/off
     F5 makes a screenshot

  4. And finally, the most important thing: when rendering with fog on,
     we cull to fog visibilityRange. Objects outside of fog
     visibilityRange are not rendered at all. This allows us to render
     very quickly when fog is on.
}

program fog_culling;

uses VectorMath, VRMLNodes, VRMLOpenGLRenderer, GL, GLU, GLWindow,
  KambiClassUtils, KambiUtils, SysUtils, Classes,
  KambiGLUtils, VRMLScene, VRMLGLScene, Cameras,
  ProgressUnit, ProgressConsole, KambiFilesUtils, VRMLErrors;

var
  Glw: TGLUIWindow;
  Scene: TVRMLGLScene;

procedure BeforeDraw(glwin: TGLWindow);
begin
 Scene.PrepareRender([tgAll], [prBoundingBox], false);
end;

type
  TFogVisibilityTester = class
    class function Test(Shape: TVRMLGLShape): boolean;
  end;

class function TFogVisibilityTester.Test(Shape: TVRMLGLShape): boolean;
begin
  { Test for collision between two spheres.
    1st is the bounding sphere of Shape.
    2nd is the sphere around current camera position,
      with the radius taken from fog visibilityRadius (we should
      always multiply this by FogDistanceScaling).
    If there is no collision than we don't have to render given Shape. }
  Result := PointsDistanceSqr(
    Shape.BoundingSphereCenter,
    (Glw.Camera As TWalkCamera).Position) <=
      Sqr(Scene.FogNode.FdVisibilityRange.Value * Scene.FogDistanceScaling +
        Sqrt(Shape.BoundingSphereRadiusSqr));
end;

procedure Draw(glwin: TGLWindow);
begin
  if Scene.Background <> nil then
  begin
    glLoadMatrix(Glw.Camera.RotationMatrix);
    Scene.Background.Render;
    glClear(GL_DEPTH_BUFFER_BIT);
  end else
    glClear(GL_COLOR_BUFFER_BIT or GL_DEPTH_BUFFER_BIT);

  glLoadMatrix(glw.Camera.Matrix);

  if Scene.Attributes.UseFog then
    Scene.Render(@TFogVisibilityTester(nil).Test, tgAll) else
    Scene.RenderFrustum(Glw.Camera.Frustum, tgAll);

  Writeln(Format('Rendered Shapes: %d / %d',
    [ Scene.LastRender_RenderedShapesCount,
      Scene.LastRender_VisibleShapesCount ]));
end;

procedure Init(glwin: TGLWindow);
begin
 glEnable(GL_LIGHT0); { headlight }

 { We use quite large triangles for fog_culling level demo wall.
   This means that fog must be correctly rendered,
   with perspective correction hint, otherwise ugly artifacts
   will be visible. }
 glHint(GL_FOG_HINT, GL_NICEST);
end;

procedure Close(glwin: TGLWindow);
begin
 Scene.GLContextClose;
end;

procedure KeyDown(glwin: TGLWindow; Key: TKey; c: char);
begin
  case Key of
    K_F:
      begin
        with Scene do Attributes.UseFog := not Attributes.UseFog;
        Glw.PostRedisplay;
      end;
    K_F5: glwin.SaveScreenDialog(FNameAutoInc('fog_culling_screen_%d.png'));
  end;
end;

procedure Resize(glwin: TGLWindow);
begin
  Scene.GLProjection(Glw.Camera, Scene.BoundingBox, Glwin.Width, Glwin.Height);
end;

type
  TDummy = class
    class function MoveAllowed(Camera: TWalkCamera;
      const ProposedNewPos: TVector3Single; out NewPos: TVector3Single;
      const BecauseOfGravity: boolean): boolean;
  end;

class function TDummy.MoveAllowed(Camera: TWalkCamera;
  const ProposedNewPos: TVector3Single; out NewPos: TVector3Single;
  const BecauseOfGravity: boolean): boolean;
begin
 Result := Scene.OctreeCollisions.MoveAllowed(
   Camera.Position, ProposedNewPos, NewPos, Camera.CameraRadius,
   BecauseOfGravity, Scene.BoundingBox, Camera.GravityUp);
end;

begin
 Glw := TGLUIWindow.Create(Application);

 Parameters.CheckHigh(0);
 try
  VRMLWarning := @VRMLWarning_Write;

  Scene := TVRMLGLScene.Create(nil);
  Scene.Load('models' + PathDelim + 'fog_culling_final.wrl');

  Writeln(Scene.Info(true, true, false));

  { build octrees }
  Progress.UserInterface := ProgressConsoleInterface;
  Scene.TriangleOctreeProgressTitle := 'Building triangle octree';
  Scene.ShapeOctreeProgressTitle := 'Building Shape octree';
  Scene.Spatial := [ssRendering, ssDynamicCollisions];

  { init Glw.Camera }
  Glw.Camera := Scene.CreateCamera(Glw, 'FLY') as TWalkCamera;
  (Glw.Camera As TWalkCamera).OnMoveAllowed := @TDummy(nil).MoveAllowed;

  Glw.OnInit := @Init;
  Glw.OnClose := @Close;
  Glw.OnResize := @Resize;
  Glw.OnBeforeDraw := @BeforeDraw;
  Glw.OnKeyDown := @KeyDown;
  Glw.InitAndRun(ProgramName, @Draw);
 finally Scene.Free end;
end.
