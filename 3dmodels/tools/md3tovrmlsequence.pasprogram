{
  Copyright 2007 Michalis Kamburelis.

  This file is part of "Kambi VRML game engine".

  "Kambi VRML game engine" is free software; you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation; either version 2 of the License, or
  (at your option) any later version.

  "Kambi VRML game engine" is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.

  You should have received a copy of the GNU General Public License
  along with "Kambi VRML game engine"; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
}

{ Convert MD3 model to a sequence of VRML models.

  This way we convert MD3 animation to a precalculated animation
  in Kanim format, see
  [http://vrmlengine.sourceforge.net/kanim_format.php].
  This may be played by
  [http://vrmlengine.sourceforge.net/view3dscene.php] (although
  view3dscene can also play MD3 directly) or
  kambi_vrml_game_engine/3dmodels.gl/examples/demo_animation example.

  For MD3 file named xxx.md3, this will generate VRML files like
  xxx_0000.wrl, xxx_0001.wrl, xxx_0002.wrl etc. and the main file
  to play whole animation xxx.kanim. }
program md3tovrmlsequence;

uses SysUtils, KambiUtils, KambiFilesUtils,
  VRMLNodes, Object3dMD3, Object3dAsVRML;

var
  Md3: TObject3dMD3;
  WWWBasePath: string;
  FileName, OutputFileName, KAnimFileName: string;
  Node: TVRMLNode;
  I: Integer;
  KAnimFile: TextFile;
begin
  Parameters.CheckHigh(1);
  FileName := Parameters[1];

  WWWBasePath := ExtractFilePath(ExpandFilename(FileName));
  Md3 := TObject3dMD3.Create(FileName);
  try

    { begin kanim file }
    KAnimFileName := DeleteFileExt(FileName) + '.kanim';
    SafeRewrite(KAnimFile, KAnimFileName);
    try
      Writeln(KAnimFile, '<?xml version="1.0"?>');
      Writeln(KAnimFile, '<animation>');

      { handle each MD3 frame }
      for I := 0 to Md3.FramesCount - 1 do
      begin
        Node := LoadMD3FrameAsVRML(Md3, I, WWWBasePath);
        try
          OutputFileName := DeleteFileExt(FileName) + Format('_%.6d.wrl', [I]);
          SaveToVRMLFile(Node, OutputFileName,
            'VRML generated by md3tovrmlsequence');
          Writeln('Wrote ', OutputFileName);
          Writeln(KAnimFile, '  <frame file_name="',
            ExtractFileName(OutputFileName), '" time="', I / 30, '" />');
        finally FreeAndNil(Node) end;
      end;

      { end kanim file }
      Writeln(KAnimFile, '</animation>');
    finally Closefile(KAnimFile) end;
    Writeln('Wrote ', KAnimFileName);

  finally FreeAndNil(Md3) end;
end.
